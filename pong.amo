' Simple Pong - Player vs AI
' Aghnar / Agima 02/2025
'
' Player 1 (left paddle)  : mouse up/down
' AI       (right paddle)  : tracks ball with slight lag
' First to 10 wins - press fire to restart
'

Screen Open 0,320,256,8,Lowres
Flash Off : Curs Off : Hide
Paper 0 : Pen 1 : Cls
Double Buffer : Autoback 0
Gr Writing 0

' Palette: blk, white, cyan, orange, yellow, red, green, grey
Palette $0,$FFF,$0BF,$F80,$FF0,$F00,$0F0,$555

' Global variables accessible inside procedures
Global CT,CB,CLL,CRR,PW,PH,BSIZ

' ---------------------
' Constants
' ---------------------
'
CT=34
CB=248
CLL=8
CRR=312
PW=6
PH=32
AISPD=3
BSIZ=8
BSDX=5
BSDY=2
BMXDX=8
WSCR=10

' ---------------------
' 7-segment digit data (bits: gfedcba)
' ---------------------
Dim SDAT(9)
SDAT(0)=63 : SDAT(1)=6 : SDAT(2)=91 : SDAT(3)=79 : SDAT(4)=102
SDAT(5)=109 : SDAT(6)=125 : SDAT(7)=7 : SDAT(8)=127 : SDAT(9)=111

' ---------------------
' Sound setup: square wave beep
' ---------------------
Reserve As Chip Work 10,2048
SA=Start(10)
For I=0 To 2047
   If (I mod 16)<8
      Poke SA+I,80
   Else
      Poke SA+I,-80
   End If
Next I

' ---------------------
' Variables
' ---------------------
'
P1X=CLL+10
P1Y=128
P2X=CRR-10-PW
P2Y=128
BX=160
BY=128
BDX=BSDX
BDY=BSDY
S1=0
S2=0
GS=0
PTMR=0
LASTSC=1

' Old positions per buffer for selective erase
Dim OBX(1),OBY(1),OP1(1),OP2(1)
OBX(0)=BX : OBY(0)=BY : OP1(0)=P1Y : OP2(0)=P2Y
OBX(1)=BX : OBY(1)=BY : OP1(1)=P1Y : OP2(1)=P2Y
BUF=0
LASTMY=Y Mouse

' ---------------------
' 7-segment digit procedure (12x20 pixels)
' ---------------------
'
Procedure PDIG[DX,DY,DG]
   Shared SDAT()
   SG=SDAT(DG)
   If SG and 1
      Bar DX+2,DY To DX+9,DY+1
   End If
   If SG and 2
      Bar DX+10,DY+2 To DX+11,DY+8
   End If
   If SG and 4
      Bar DX+10,DY+11 To DX+11,DY+17
   End If
   If SG and 8
      Bar DX+2,DY+18 To DX+9,DY+19
   End If
   If SG and 16
      Bar DX,DY+11 To DX+1,DY+17
   End If
   If SG and 32
      Bar DX,DY+2 To DX+1,DY+8
   End If
   If SG and 64
      Bar DX+2,DY+9 To DX+9,DY+10
   End If
End Proc

' ---------------------
' Court drawing for title screen (with dashes)
' ---------------------
'
Procedure PCOURT
   Ink 1
   Bar 0,CT To 319,CT+2
   Bar 0,CB-2 To 319,CB
   Ink 7
   For YY=CT+6 To CB-12 Step 16
      Bar 158,YY To 161,YY+6
   Next YY
End Proc

' ---------------------
' Title screen
' ---------------------
'
Cls 0
PCOURT
Pen 4 : Paper 0
Locate 15,6 : Print "AMIGA PONG";
Pen 2
Locate 9,10 : Print "PLAYER 1  vs  COMPUTER";
Pen 6
Locate 12,14 : Print "FIRST TO 10 WINS";
Pen 5
Locate 11,18 : Print "PRESS FIRE TO START";
Pen 7
Locate 15,22 : Print "ESC TO QUIT";
Screen Swap : Wait Vbl

' Wait for fire button or mouse
While Fire(1)=0 and Mouse Key=0 : Wait Vbl : Wend
While Fire(1) or Mouse Key>0 : Wait Vbl : Wend

' ---------------------
' Setup: draw court on both buffers
' ---------------------
'
Cls 0
Ink 1
Bar 0,CT To 319,CT+2
Bar 0,CB-2 To 319,CB
Ink 7
Draw 160,CT+3 To 160,CB-3
Screen Swap : Wait Vbl
Cls 0
Ink 1
Bar 0,CT To 319,CT+2
Bar 0,CB-2 To 319,CB
Ink 7
Draw 160,CT+3 To 160,CB-3

' ---------------------
' Main game loop
' ---------------------
'
Do

   ' --- ESC to quit ---
   If Key State(69)
      Sam Stop
      End
   End If

   ' --- Erase old positions on this buffer ---
   Ink 0
   Bar OBX(BUF),OBY(BUF) To OBX(BUF)+BSIZ,OBY(BUF)+BSIZ
   Bar P1X,OP1(BUF) To P1X+PW,OP1(BUF)+PH
   Bar P2X,OP2(BUF) To P2X+PW,OP2(BUF)+PH
   Bar 0,0 To 319,CT-2
   Bar 0,120 To 319,175

   ' Redraw court elements that may have been erased
   Ink 1
   Bar 0,CT To 319,CT+2
   Bar 0,CB-2 To 319,CB
   Ink 7
   Draw 160,CT+3 To 160,CB-3

   ' --- Input ---
   If GS=0
      ' Mouse control for player 1 (relative movement)
      MY=Y Mouse
      DY=MY-LASTMY
      LASTMY=MY
      Add P1Y,DY
      If P1Y<CT+3
         P1Y=CT+3
      End If
      If P1Y>CB-PH-3
         P1Y=CB-PH-3
      End If

      ' --- AI ---
      BMID=BY+BSIZ/2
      PMID=P2Y+PH/2
      If BMID>PMID+2
         Add P2Y,AISPD
      Else If BMID<PMID-2
         Add P2Y,-AISPD
      End If
      If P2Y<CT+3
         P2Y=CT+3
      End If
      If P2Y>CB-PH-3
         P2Y=CB-PH-3
      End If

      ' --- Move ball ---
      Add BX,BDX
      Add BY,BDY

      ' --- Ball vs top/bottom walls ---
      If BY<=CT+3
         BY=CT+3
         BDY=-BDY
         Sam Raw 1,SA,1024,6400
      End If
      If BY+BSIZ>=CB-3
         BY=CB-3-BSIZ
         BDY=-BDY
         Sam Raw 1,SA,1024,6400
      End If

      ' --- Ball vs player 1 paddle ---
      If BDX<0
         If BX<=P1X+PW and BX+BSIZ>=P1X
            If BY+BSIZ>=P1Y and BY<=P1Y+PH
               BX=P1X+PW+1
               BDX=-BDX
               Sam Raw 1,SA,1024,8000
               If BDX<BMXDX
                  Inc BDX
               End If
               HP=BY+BSIZ/2-P1Y-PH/2
               BDY=HP/4
               If BDY=0 and BY>P1Y+PH/2
                  BDY=1
               End If
               If BDY=0 and BY<P1Y+PH/2
                  BDY=-1
               End If
            End If
         End If
      End If

      ' --- Ball vs AI paddle ---
      If BDX>0
         If BX+BSIZ>=P2X and BX<=P2X+PW
            If BY+BSIZ>=P2Y and BY<=P2Y+PH
               BX=P2X-BSIZ-1
               BDX=-BDX
               Sam Raw 1,SA,1024,8000
               If BDX>-BMXDX
                  Dec BDX
               End If
               HP=BY+BSIZ/2-P2Y-PH/2
               BDY=HP/4
               If BDY=0 and BY>P2Y+PH/2
                  BDY=1
               End If
               If BDY=0 and BY<P2Y+PH/2
                  BDY=-1
               End If
            End If
         End If
      End If

      ' --- Scoring ---
      If BX<CLL
         Inc S2
         LASTSC=2
         GS=1
         PTMR=50
         Sam Raw 1,SA,1024,4800
      Else If BX+BSIZ>CRR
         Inc S1
         LASTSC=1
         GS=1
         PTMR=50
         Sam Raw 1,SA,1024,4800
      End If

      If S1>=WSCR or S2>=WSCR
         GS=2
      End If

   Else If GS=1
      Dec PTMR
      If PTMR<=0
         BX=158
         BY=128
         If LASTSC=1
            BDX=BSDX
         Else
            BDX=-BSDX
         End If
         BDY=BSDY
         If Rnd(1)=1
            BDY=-BDY
         End If
         GS=0
      End If

   Else If GS=2
      If Fire(1) or Mouse Key>0
         While Fire(1) or Mouse Key>0 : Wait Vbl : Wend
         S1=0
         S2=0
         P1Y=128
         P2Y=128
         BX=158
         BY=128
         BDX=BSDX
         BDY=BSDY
         LASTSC=1
         GS=0
      End If
   End If

   ' --- Store positions for this buffer ---
   OBX(BUF)=BX : OBY(BUF)=BY
   OP1(BUF)=P1Y : OP2(BUF)=P2Y

   ' --- Draw ---
   ' Player paddle (cyan)
   Ink 2
   Bar P1X,P1Y To P1X+PW,P1Y+PH

   ' AI paddle (orange)
   Ink 3
   Bar P2X,P2Y To P2X+PW,P2Y+PH

   ' Ball (yellow)
   Ink 4
   Bar BX,BY To BX+BSIZ,BY+BSIZ

   ' 7-segment score display
   Ink 6
   If S1>=10
      PDIG[118,7,1]
      PDIG[134,7,0]
   Else
      PDIG[126,7,S1]
   End If
   ' Dash
   Ink 1
   Bar 152,15 To 162,18
   ' Right score
   Ink 6
   If S2>=10
      PDIG[172,7,1]
      PDIG[188,7,0]
   Else
      PDIG[180,7,S2]
   End If

   ' Status messages
   If GS=1
      Pen 5 : Paper 0
      Locate 17,17 : Print "POINT!";
   End If
   If GS=2
      If S1>=WSCR
         Pen 6 : Paper 0
         Locate 13,16 : Print "PLAYER 1 WINS!";
      Else
         Pen 5 : Paper 0
         Locate 13,16 : Print "COMPUTER WINS!";
      End If
      Pen 4 : Paper 0
      Locate 8,20 : Print "PRESS FIRE TO PLAY AGAIN";
   End If

   BUF=1-BUF
   Screen Swap : Wait Vbl

Loop
